dependencies {
    compile fileTree(dir: 'libs', include: ['protobuf-java-kornilova-l-3.5.1.jar'])
}

repositories {
    repositories {
        mavenCentral()
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.5.1'
    }

    generatedFilesBaseDir = "$projectDir/generated"

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {}
                js {}
            }
        }
    }
}

sourceSets.main.java.srcDir new File(protobuf.generatedFilesBaseDir + '/main/java')
idea {
    module {
        generatedSourceDirs += file(protobuf.generatedFilesBaseDir + '/main/java')
    }
}

/**
 * Rename imports in generated java sources
 * from com.google.protobuf
 * to com.github.kornilova_l.libs.com.google.protobuf
 */
task renameProtobufPackage {
    doLast {
        ant.replaceregexp(match: '(?<!com\\.github\\.kornilova_l\\.libs\\.)com\\.google\\.protobuf',
                replace: 'com.github.kornilova_l.libs.com.google.protobuf', flags: 'g', byline: true) {
            fileset(dir: protobuf.generatedFilesBaseDir + '/main/java')
        }
    }
}

compileJava.dependsOn renameProtobufPackage

task copyJSProto(type: Copy) {
    from protobuf.generatedFilesBaseDir + '/main/js'
    into '../visualization/static/js/generated'
}
