plugins {
    id 'com.moowork.node' version '1.3.1'
    id 'com.liferay.soy' version '3.1.9'
}

repositories {
    mavenCentral()
}

node {
    version = '12.16.1'
    npmVersion = '6.14.4'
    download = true
    nodeModulesDir = file('.')
}

task copyJSProto(type: Copy, dependsOn: ':proto:compileKotlin') {
    from '../proto/generated/main/js'
    into 'static/js/generated'
}

def npmRun(String command) {
    return tasks.create(command, NpmTask) {
        args = ['run', command]
        dependsOn = [copyJSProto, npmInstall]
    }
}

task browserify(dependsOn: [npmRun('browserify-accumulative-trees'),
                            npmRun('browserify-trees-preview'),
                            npmRun('browserify-call-tree')]) {
}

browserify.dependsOn npmInstall

buildSoy {
    source 'static/tree-templates.soy'
}

task moveGeneratedTemplates(dependsOn: buildSoy) {
    doLast {
        file('static/tree-templates.soy.js').renameTo(file('static/js/generated/tree-templates.js'))
    }
}

task copyStatic(dependsOn: [':visualization:browserify', ':visualization:moveGeneratedTemplates']) {
    doLast {
        copy {
            from 'static'
            into '../core/src/main/generated-resources/static'
        }
    }
}
