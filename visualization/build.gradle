plugins {
    id 'com.moowork.node' version '1.2.0'
    id 'com.liferay.soy' version '3.1.6'
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

node {
    version = '10.6.0'
    npmVersion = '6.1.0'
    download = true
    nodeModulesDir = file('.')
}

task copyJSProto(type: Copy, dependsOn: ':proto:generateProto') {
    from '../proto/generated/main/js'
    into 'static/js/generated'
}

def npmRun(String command) {
    return tasks.create(command, NpmTask) {
        args = ['run', command]
        dependsOn = [copyJSProto, npmInstall]
    }
}

task browserify(dependsOn: [npmRun('browserify-accumulative-trees'),
                            npmRun('browserify-trees-preview'),
                            npmRun('browserify-call-tree')]) {
}

browserify.dependsOn npmInstall

buildSoy {
    source 'static/tree-templates.soy'
}

task moveGeneratedTemplates(dependsOn: buildSoy) {
    doLast {
        file('static/tree-templates.soy.js').renameTo(file('static/js/generated/tree-templates.js'))
    }
}
