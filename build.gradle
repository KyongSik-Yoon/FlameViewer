buildscript {
    ext.kotlin_version = '1.2.20'
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url 'http://dl.bintray.com/jetbrains/intellij-plugin-service'
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "me.champeau.gradle:jmh-gradle-plugin:0.3.1"
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.1.10"
}

apply plugin: 'org.jetbrains.intellij'
apply plugin: "idea"
apply plugin: 'me.champeau.gradle.jmh'

allprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    group = 'com.gitgub.kornilova_l'
    version = '0.6'
}

repositories {
    mavenCentral()
}

configurations {
    intellij.updateSinceUntilBuild = false
    intellij.downloadSources = false
    intellij.pluginName = "FlamegraphProfiler"
}

dependencies {
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.1'
    compile fileTree(dir: 'lib', include: ['com.jrockit.mc.flightrecorder_5.5.1.172852.jar'])
    compile fileTree(dir: 'lib', include: ['yourkit-to-flamegraph.jar'])
    compile fileTree(dir: 'lib', include: ['com.jrockit.mc.common_5.5.1.172852.jar'])
    compile fileTree(dir: 'lib', include: ['flight-recorder-parser-for-java-9.jar'])
    compile project('configuration')
    compile project('protobuf')
    testCompile project('agent')
}


task copyStatic(type: Copy) {
    from 'visualization/static'
    into 'build/classes/main/static'
}

task copyAgent(type: Copy) {
    from 'agent/build/libs'
    into 'build/classes/main'
}

task('prepareAndRunIde', dependsOn: [copyStatic, build, jar]) {
    doLast {
        runIdea.execute()
    }
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

/* if have some problems with jmh - execute clean first */
jmh {
    /* jmh Jar warns about duplicate dependencies
     * currently I do not know how to fix it */
    zip64 = true
    duplicateClassesStrategy = DuplicatesStrategy.WARN
    include = 'benchmarks.CallTracesBenchmark'
}