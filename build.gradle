version '1.0'

apply plugin: 'java'
apply plugin: 'application'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "me.champeau.gradle:jmh-gradle-plugin:0.3.1"
    }
}

apply plugin: "me.champeau.gradle.jmh"

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile "org.ow2.asm:asm:5.2"
    compile "org.ow2.asm:asm-util:5.2" // TraceClassVisitor (for debugging)
    compile group: 'org.ow2.asm', name: 'asm-commons', version: '5.0.3' // LocalVariablesSorter
}

mainClassName = 'org.jetbrains.test.Main'

task packAgent (type: Jar, dependsOn: classes) {
    manifest {
        attributes "Premain-Class": "profiler.ProfilingAgent"
    }
    archiveName = 'javaagent.jar'
    from sourceSets.main.output
    include '**/profiler/*'
    includeEmptyDirs = false
}

task profileClass(type: JavaExec, dependsOn: [build, packAgent]) {
    classpath = sourceSets.test.runtimeClasspath
//    main = "org.jetbrains.test.SimpleExample"
    main = "org.jetbrains.test.TestApplicationWithoutSleep"
    jvmArgs += ["-javaagent:${packAgent.getArchivePath()}"]
}

jmh {
    packAgent.execute()
    warmupIterations = 5
    iterations = 10
    fork = 2
//    include = "simpleExample"
    include = "testApplicationWithoutSleep"
    jvmArgs = "-javaagent:${packAgent.getArchivePath()}"
}
