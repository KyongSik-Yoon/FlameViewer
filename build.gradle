import io.netifi.flatbuffers.plugin.tasks.FlatBuffers

buildscript {
    ext.kotlin_version = '1.3.10'
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url 'http://dl.bintray.com/jetbrains/intellij-plugin-service'
        }
        maven {
            url 'https://repository-cdn.liferay.com/nexus/content/groups/public'
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.6'
        classpath 'com.moowork.gradle:gradle-node-plugin:1.2.0'
        classpath 'com.liferay:com.liferay.gradle.plugins.soy:3.1.6'
    }
}

plugins {
    id 'org.jetbrains.intellij' version '0.1.10'
    id 'com.google.protobuf' version '0.8.6'
    id 'com.moowork.node' version '1.2.0'
    id 'com.liferay.soy' version '3.1.6'
    id "io.netifi.flatbuffers" version "1.0.4"
    id 'idea'
}

apply plugin: 'java'
apply plugin: 'kotlin'
group = 'com.github.korniloval'
version = '0.8.2-SNAPSHOT'

repositories {
    mavenCentral()
}

configurations {
    intellij.updateSinceUntilBuild = false
    intellij.downloadSources = false
    intellij.pluginName = "FlameViewer"
    intellij.version = "2018.2"
}

dependencies {
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.1'
    compile group: 'com.github.davidmoten', name: 'flatbuffers-java', version: '1.9.0.1'
    compile fileTree(dir: 'lib', include: ['com.jrockit.mc.flightrecorder_5.5.1.172852.jar'])
    compile fileTree(dir: 'lib', include: ['com.jrockit.mc.common_5.5.1.172852.jar'])
    compile fileTree(dir: 'lib', include: ['flight-recorder-parser-for-java-9.jar'])
    compile fileTree(dir: 'lib', include: ['protobuf-java-kornilova-l-3.5.1.jar'])
    testCompile group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.11.0'
}

node {
    version = '10.6.0'
    npmVersion = '6.1.0'
    download = true
    nodeModulesDir = file('visualization')
}

def npmRun(String command) {
    return tasks.create(command, NpmTask) {
        args = ['run', command]
        dependsOn = ["copyJSProto", npmInstall]
    }
}

task browserify(dependsOn: [npmRun('browserify-accumulative-trees'),
                            npmRun('browserify-trees-preview'),
                            npmRun('browserify-call-tree')]) {
}

browserify.dependsOn npmInstall

buildSoy {
    source 'visualization/static/tree-templates.soy'
}

task moveGeneratedTemplates(dependsOn: buildSoy) {
    doLast {
        file('visualization/static/tree-templates.soy.js').renameTo(file('visualization/static/js/generated/tree-templates.js'))
    }
}

task copyStatic(type: Copy, dependsOn: [browserify, moveGeneratedTemplates]) {
    from 'visualization/static'
    into 'src/main/generated-resources/static'
    doLast {
        /* this is for running tests from IDEA interface */
        mkdir "out/test/classes/static"
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

flatbuffers {
    flatcPath = '/usr/local/bin/flatc'
    language = 'java'
}

task compileFlatBuffers(type: FlatBuffers) {
    inputDir = file("src/main/flatbuffers")
    outputDir = file("src/gen/java")
}

sourceSets.main.resources.srcDirs "src/main/resources", "src/main/generated-resources"

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.5.1'
    }

    generatedFilesBaseDir = "$projectDir/generated"

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {}
                js {
                    option 'import_style=commonjs'
                    option 'binary'
                }
            }
            if (task.name == 'generateProto') {
                copyJSProto.dependsOn task
                renameProtobufPackage.dependsOn task
            }
        }
    }
}

sourceSets.main.java.srcDir new File(protobuf.generatedFilesBaseDir + '/main/java')
sourceSets.main.java.srcDir file('src/gen/java')
idea {
    module {
        generatedSourceDirs += file('src/gen/java')
        generatedSourceDirs += file(protobuf.generatedFilesBaseDir + '/main/java')
    }
}

/**
 * Rename imports in generated java sources
 * from com.google.protobuf
 * to com.github.kornilova_l.libs.com.google.protobuf
 */
task renameProtobufPackage { // depends on generateProto task (dependency is set above)
    doLast {
        ant.replaceregexp(match: '(?<!com\\.github\\.kornilova_l\\.libs\\.)com\\.google\\.protobuf',
                replace: 'com.github.kornilova_l.libs.com.google.protobuf', flags: 'g', byline: true) {
            fileset(dir: protobuf.generatedFilesBaseDir + '/main/java')
        }
    }
}

compileKotlin.dependsOn renameProtobufPackage

task copyJSProto(type: Copy, dependsOn: renameProtobufPackage) {
    from protobuf.generatedFilesBaseDir + '/main/js'
    into 'visualization/static/js/generated'
}
